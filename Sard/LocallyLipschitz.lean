import Sard.ToSubset
import Mathlib.Analysis.Calculus.ContDiff
import Mathlib.Topology.MetricSpace.Lipschitz
import Mathlib.Topology.Basic
import Mathlib.Order.Filter.Basic

/-!
## Locally Lipschitz functions
Define locally Lipschitz functions and show their elementary properties.
Show that C¬π functions are locally Lipschitz.
-/
-- STATUS. This file has mostly been PRed to mathlib; refactors there are not merged here.

open Function NNReal Set Topology
set_option autoImplicit false

variable {X Y Z: Type*}

section EMetric
variable [EMetricSpace X] [EMetricSpace Y] [EMetricSpace Z]

/-- `f : X ‚Üí Y` is **locally Lipschitz** iff every point `p ‚àà X` has a neighourhood
on which `f` is Lipschitz. -/
def LocallyLipschitz (f : X ‚Üí Y) : Prop := ‚àÄ x : X, ‚àÉ K, ‚àÉ t ‚àà ùìù x, LipschitzOnWith K f t

namespace LocallyLipschitz

/-- A Lipschitz function is locally Lipschitz. -/
protected lemma of_Lipschitz {f : X ‚Üí Y} {K : ‚Ñù‚â•0} (hf : LipschitzWith K f) : LocallyLipschitz f := by
  intro x
  use K, univ
  rw [lipschitzOn_univ]
  exact ‚ü®Filter.univ_mem, hf‚ü©

/-- The identity function is locally Lipschitz. -/
protected lemma id : LocallyLipschitz (@id X) := LocallyLipschitz.of_Lipschitz (LipschitzWith.id)

/-- Constant functions are locally Lipschitz. -/
protected lemma const (b : Y) : LocallyLipschitz (fun _ : X ‚Ü¶ b) :=
  LocallyLipschitz.of_Lipschitz (LipschitzWith.const b)

/-- A locally Lipschitz function is continuous. (The converse is false: for example,
$x ‚Ü¶ \sqrt{x}$ is continuous, but not locally Lipschitz at 0.) -/
protected theorem continuous {f : X ‚Üí Y} (hf : LocallyLipschitz f) : Continuous f := by
  apply Iff.mpr continuous_iff_continuousAt
  intro x
  rcases (hf x) with ‚ü®K, t, ht, hK‚ü©
  exact ContinuousOn.continuousAt (LipschitzOnWith.continuousOn hK) ht

-- tweaked version of the result in mathlib, weaker hypotheses -- not just restricting the domain,
-- but also weakening the assumption on the codomain
theorem comp_lipschitzOnWith' {Kf Kg : ‚Ñù‚â•0} {f : Y ‚Üí Z} {g : X ‚Üí Y} {s : Set X}
    (hf : LipschitzOnWith Kf f (g '' s)) (hg : LipschitzOnWith Kg g s) :
    LipschitzOnWith (Kf * Kg) (f ‚àò g) s := by
  intro x hx y hy
  calc edist ((f ‚àò g) x) ((f ‚àò g) y)
    _ ‚â§ Kf * edist (g x) (g y) := hf (mem_image_of_mem g hx) (mem_image_of_mem g hy)
    _ ‚â§ Kf * (Kg * edist x y) := by exact mul_le_mul_left' (hg hx hy) Kf
    _ = ‚Üë(Kf * Kg) * edist x y := by rw [‚Üê mul_assoc, ENNReal.coe_mul]

/-- The composition of locally Lipschitz functions is locally Lipschitz. --/
protected lemma comp  {f : Y ‚Üí Z} {g : X ‚Üí Y}
    (hf : LocallyLipschitz f) (hg : LocallyLipschitz g) : LocallyLipschitz (f ‚àò g) := by
  intro x
  -- g is Lipschitz on t ‚àã x, f is Lipschitz on u ‚àã g(x)
  rcases hg x with ‚ü®Kg, t, ht, hgL‚ü©
  rcases hf (g x) with ‚ü®Kf, u, hu, hfL‚ü©
  -- idea: shrink u to g(t), then apply `comp_lipschitzOnWith'`
  -- more precisely: restrict g to t' := t ‚à© g‚Åª¬π(u); the preimage of u under g':=g‚à£t.
  let g' := t.restrict g
  let t'' := (g' ‚Åª¬π' u)
  let t' : Set X := { (x : X) | x ‚àà t'' }
  have h‚ÇÅ : t' = t ‚à© g ‚Åª¬π' u := by
    apply Set.Subset.antisymm_iff.mpr
    constructor
    ¬∑ intro x hx
      simp at hx
      obtain ‚ü®x, ha1, ha2, rfl‚ü© := hx
      exact ‚ü®by assumption, by simp only [mem_preimage, ha1]‚ü©
    ¬∑ rintro x ‚ü®ht, hgu‚ü©
      simp only [mem_preimage] at hgu
      simp says simp only [mem_preimage, restrict_apply, Subtype.exists, exists_and_left, exists_prop, mem_setOf_eq]
      use x
  have h‚ÇÇ : t' ‚àà ùìù x := by -- FIXME: the following is a tour de force; there must be a nicer proof
    -- by ht, t contains an open subset U
    rcases (Iff.mp (mem_nhds_iff) ht) with ‚ü®U, hUt, hUopen, hxU‚ü©
    -- similarly, u contains an open subset V
    rcases (Iff.mp (mem_nhds_iff) hu) with ‚ü®V, hVt, hVopen, hgxV‚ü©
    -- by continuity, g‚Åª¬π(u) contains the open subset g‚Åª¬π(V)
    have : ContinuousOn g U := LipschitzOnWith.continuousOn (LipschitzOnWith.mono hgL hUt)
    have h : IsOpen (U ‚à© (g ‚Åª¬π' V)) := ContinuousOn.preimage_open_of_open this hUopen hVopen
    have : U ‚à© (g ‚Åª¬π' V) ‚äÜ t' := by rw [h‚ÇÅ]; apply inter_subset_inter hUt (preimage_mono hVt)
    -- now, U ‚à© g‚Åª¬π(V) is an open subset contained in t'
    rw [mem_nhds_iff]
    use U ‚à© (g ‚Åª¬π' V)
    exact ‚ü®this, ‚ü®h, ‚ü®hxU, hgxV‚ü©‚ü©‚ü©
  have : g '' t' ‚äÜ u := by calc g '' t'
    _ = g '' (t ‚à© g ‚Åª¬π' u) := by rw [h‚ÇÅ]
    _ ‚äÜ g '' t ‚à© g '' (g ‚Åª¬π' u) := by apply image_inter_subset
    _ ‚äÜ g '' t ‚à© u := by gcongr; apply image_preimage_subset
    _ ‚äÜ u := by apply inter_subset_right
  use Kf * Kg, t'
  have h: t' ‚äÜ t := by rw [h‚ÇÅ]; exact inter_subset_left t (g ‚Åª¬π' u)
  exact ‚ü®h‚ÇÇ, comp_lipschitzOnWith'
    (LipschitzOnWith.mono hfL this) (LipschitzOnWith.mono hgL h)‚ü©

/-- If `f` and `g` are locally Lipschitz, so is the induced map `f √ó g` to the product type. -/
protected lemma prod {f : X ‚Üí Y} (hf : LocallyLipschitz f) {g : X ‚Üí Z} (hg : LocallyLipschitz g) :
    LocallyLipschitz fun x => (f x, g x) := by
  intro x
  rcases hf x with ‚ü®Kf, t‚ÇÅ, h‚ÇÅt, hfL‚ü©
  rcases hg x with ‚ü®Kg, t‚ÇÇ, h‚ÇÇt, hgL‚ü©
  use max Kf Kg, t‚ÇÅ ‚à© t‚ÇÇ
  constructor
  ¬∑ exact Filter.inter_mem h‚ÇÅt h‚ÇÇt
  ¬∑ intro y hy z hz
    have h‚ÇÅ : edist (f y) (f z) ‚â§ Kf * edist y z := by
      exact LipschitzOnWith.mono hfL (inter_subset_left t‚ÇÅ t‚ÇÇ) hy hz
    have h‚ÇÇ : edist (g y) (g z) ‚â§ Kg * edist y z := by
      exact LipschitzOnWith.mono hgL (inter_subset_right t‚ÇÅ t‚ÇÇ) hy hz
    rw [ENNReal.coe_mono.map_max, Prod.edist_eq, ENNReal.max_mul]
    exact max_le_max h‚ÇÅ h‚ÇÇ

protected theorem prod_mk_left (a : X) : LocallyLipschitz (Prod.mk a : Y ‚Üí X √ó Y) :=
  LocallyLipschitz.of_Lipschitz (LipschitzWith.prod_mk_left a)

protected theorem prod_mk_right (b : Y) : LocallyLipschitz (fun a : X => (a, b)) :=
  LocallyLipschitz.of_Lipschitz (LipschitzWith.prod_mk_right b)

protected theorem iterate {f : X ‚Üí X} (hf : LocallyLipschitz f) : ‚àÄ n, LocallyLipschitz f^[n]
  | 0 => by simpa only [pow_zero] using LocallyLipschitz.id
  | n + 1 => by rw [iterate_add, iterate_one]; exact (LocallyLipschitz.iterate hf n).comp hf

protected theorem mul {f g : Function.End X} (hf : LocallyLipschitz f)
    (hg : LocallyLipschitz g) : LocallyLipschitz (f * g : Function.End X) := hf.comp hg

protected theorem pow {f : Function.End X} (h : LocallyLipschitz f) :
    ‚àÄ n : ‚Ñï, LocallyLipschitz (f ^ n : Function.End X)
  | 0 => by simpa only [pow_zero] using LocallyLipschitz.id
  | n + 1 => by
    rw [pow_succ]
    exact h.mul (LocallyLipschitz.pow h n)
end LocallyLipschitz
end EMetric

--------- everything above here has been PRed

section Metric
variable [MetricSpace X] [MetricSpace Y] [MetricSpace Z]
namespace LocallyLipschitz
/-- `toSubset` is compatible with the neighbourhood filter. -/
protected lemma ToSubset.compatible_with_nhds (s t : Set X) {x : s} (ht : t ‚àà ùìù ‚Üëx) : toSubset t s ‚àà ùìù x := by sorry

/-- `toSubset` is compatible with the "neighbourhood within" filter. -/
protected lemma ToSubset.compatible_with_nhds_within (t U: Set X) {x : U} (hU : IsOpen U) (ht : t ‚àà ùìù[U] ‚Üëx) :
    toSubset t U ‚àà ùìù x := by
  have : t ‚à© U ‚àà ùìù ‚Üëx := by
    -- ùìù[U] ‚Üëx is the "neighbourhood within" filter, consisting of all sets t ‚äá U ‚à© b
    -- for some neighbourhood b of x. Choose an open subset a ‚äÜ b,
    -- then a ‚à© U is an open subset contained in t.
    rcases ht with ‚ü®b, hb, U', hU', htaU‚ü©
    rw [mem_nhds_iff] at hb
    rcases hb with ‚ü®a, ha, haopen, hxa‚ü©
    rw [mem_nhds_iff]
    use a ‚à© U
    constructor
    ¬∑ calc a ‚à© U
        _ ‚äÜ b ‚à© U := inter_subset_inter_left U ha
        _ = b ‚à© (U' ‚à© U) := by congr; rw [(Iff.mpr inter_eq_right_iff_subset hU')]
        _ ‚äÜ (b ‚à© U') ‚à© U := by rw [inter_assoc]
        _ = t ‚à© U := by rw [htaU]
    ¬∑ exact ‚ü®IsOpen.inter haopen hU, ‚ü®hxa, Subtype.mem x‚ü©‚ü©
  apply ToSubset.compatible_with_nhds
  exact Filter.mem_of_superset this (inter_subset_left t U)

/- Restrictions of Lipschitz functions is compatible with taking subtypes. -/
protected lemma LipschitzOnWith.restrict_subtype {f : X ‚Üí Y} {K : ‚Ñù‚â•0} (s t : Set X)
    (hf : LipschitzOnWith K f t) : LipschitzOnWith K (restrict s f) (toSubset t s) :=
  fun _ hx _ hy ‚Ü¶ hf hx hy

/-- Restrictions of locally Lipschitz functions are locally Lipschitz. -/
protected lemma restrict {f : X ‚Üí Y} (hf : LocallyLipschitz f) (s : Set X) :
    LocallyLipschitz (s.restrict f) := by
  intro x
  rcases hf x with ‚ü®K, t, ht, hfL‚ü©
  -- Consider t' := t ‚à© s as a neighbourhood of x *in s*.
  use K, toSubset t s
  exact ‚ü®ToSubset.compatible_with_nhds s t ht, LipschitzOnWith.restrict_subtype s t hfL‚ü©

/-- C¬π functions are locally Lipschitz. -/
-- upstreamed, as ContDiff.locallyLipschitz
protected lemma of_C1 {E F: Type*} {f : E ‚Üí F} [NormedAddCommGroup E] [NormedSpace ‚Ñù E]
    [NormedAddCommGroup F] [NormedSpace ‚Ñù F] (hf : ContDiff ‚Ñù 1 f) : LocallyLipschitz f := by
  intro x
  rcases (ContDiffAt.exists_lipschitzOnWith (ContDiff.contDiffAt hf)) with ‚ü®K, t, ht, hf‚ü©
  use K, t

/-- If `f` is C¬π on an open convex set `U`, the restriction of `f` to `U` is locally Lipschitz. -/
-- TODO: upstream to ContDiffOn.lean! once the restriction stuff is figured out. can drop the variables there :-)
lemma of_C1_on_open {E F: Type*} {f : E ‚Üí F} [NormedAddCommGroup E] [NormedSpace ‚Ñù E]
    [NormedAddCommGroup F] [NormedSpace ‚Ñù F] {U : Set E} (h‚ÇÅU : IsOpen U) (h‚ÇÇU : Convex ‚Ñù U)
    (hf : ContDiffOn ‚Ñù 1 f U) : LocallyLipschitz (U.restrict f) := by
  intro x
  have : ContDiffWithinAt ‚Ñù 1 f U x := ContDiffOn.contDiffWithinAt hf (Subtype.mem x)
  let h := ContDiffWithinAt.exists_lipschitzOnWith this
  rcases (h h‚ÇÇU) with ‚ü®K, t, ht, hf‚ü©
  -- `t` is a neighbourhood of x "within U", i.e. contains the intersection of U with some nbhd a of x.
  -- Intersect with `U` to obtain a neighbourhood contained in `U`.
  use K, toSubset t U
  exact ‚ü®ToSubset.compatible_with_nhds_within t U h‚ÇÅU ht, LipschitzOnWith.restrict_subtype U t hf‚ü©
end LocallyLipschitz
end Metric

section EMetric
namespace LocallyLipschitz
---------------------------------
-- all of this section has been PRed, except for the sum and smul lemmas
---------------------------------
variable [MetricSpace X] [MetricSpace Y] [MetricSpace Z] {f g : X ‚Üí ‚Ñù}
/-- The minimum of locally Lipschitz functions is locally Lipschitz. -/
protected lemma min (hf : LocallyLipschitz f) (hg : LocallyLipschitz g) :
    LocallyLipschitz (fun x => min (f x) (g x)) := by
  let m : ‚Ñù √ó ‚Ñù ‚Üí ‚Ñù := fun p ‚Ü¶ min p.1 p.2
  have h : LocallyLipschitz m := LocallyLipschitz.of_Lipschitz lipschitzWith_min
  exact LocallyLipschitz.comp h (LocallyLipschitz.prod hf hg)

/-- The maximum of locally Lipschitz functions is locally Lipschitz. -/
protected lemma max (hf : LocallyLipschitz f) (hg : LocallyLipschitz g) :
    LocallyLipschitz (fun x => max (f x) (g x)) := by
  let m : ‚Ñù √ó ‚Ñù ‚Üí ‚Ñù := fun p ‚Ü¶ max p.1 p.2
  have h : LocallyLipschitz m := LocallyLipschitz.of_Lipschitz lipschitzWith_max
  exact LocallyLipschitz.comp h (LocallyLipschitz.prod hf hg)

theorem max_const (hf : LocallyLipschitz f) (a : ‚Ñù) : LocallyLipschitz fun x => max (f x) a :=
  LocallyLipschitz.max hf (LocallyLipschitz.const a)

theorem const_max (hf : LocallyLipschitz f) (a : ‚Ñù) : LocallyLipschitz fun x => max a (f x) :=
  LocallyLipschitz.max (LocallyLipschitz.const a) hf

theorem min_const (hf : LocallyLipschitz f) (a : ‚Ñù) : LocallyLipschitz fun x => min (f x) a :=
  LocallyLipschitz.min hf (LocallyLipschitz.const a)

theorem const_min (hf : LocallyLipschitz f) (a : ‚Ñù) : LocallyLipschitz fun x => min a (f x) :=
  LocallyLipschitz.min (LocallyLipschitz.const a) hf
end LocallyLipschitz
end EMetric

section Normed
variable [MetricSpace X] [NormedAddCommGroup Y] [NormedSpace ‚Ñù Y] {f g : X ‚Üí Y}

/-- The sum of Lipschitz functions is Lipschitz. -/
protected lemma LipschitzOnWith.sum {Kf : ‚Ñù‚â•0} {Kg : ‚Ñù‚â•0} {s : Set X}
    (hf : LipschitzOnWith Kf f s) (hg : LipschitzOnWith Kg g s) :
    LipschitzOnWith (Kf + Kg) (f + g) s := by
  intro y hy z hz
  -- Since Y is normed, the distance is translation-invariant.
  have translation: ‚àÄ w w' w'' : Y, edist (w + w'') (w' + w'') = edist w w' := by
    intro w w' w''
    simp only [edist_add_right]
  simp only [Pi.add_apply, ENNReal.coe_add]
  calc edist (f y + g y) (f z + g z)
    _ ‚â§ edist (f y + g y) (g y + f z) + edist (g y + f z) (f z + g z) := by apply edist_triangle
    _ = edist (f y + g y) (f z + g y) + edist (g y + f z) (g z + f z) := by
        simp only [add_comm, edist_add_right, edist_add_left]
    _ ‚â§ edist (f y) (f z) + edist (g y) (g z) := by rw [translation, translation]
    _ ‚â§ Kf * edist y z + Kg * edist y z := add_le_add (hf hy hz) (hg hy hz)
    _ = (Kf + Kg) * edist y z := by ring

/-- The sum of Lipschitz functions on `s` is Lipschitz on `s`. -/
protected lemma LipschitzWith.sum {Kf : ‚Ñù‚â•0} {Kg : ‚Ñù‚â•0}
    (hf : LipschitzWith Kf f) (hg : LipschitzWith Kg g) : LipschitzWith (Kf + Kg) (f + g) :=
  lipschitzOn_univ.mp ((lipschitzOn_univ.mpr hf).sum (lipschitzOn_univ.mpr hg))

/-- The sum of locally Lipschitz functions is locally Lipschitz. -/
protected lemma LocallyLipschitz.sum (hf : LocallyLipschitz f) (hg : LocallyLipschitz g) :
    LocallyLipschitz (f + g) := by
  intro x
  rcases hf x with ‚ü®Kf, t‚ÇÅ, h‚ÇÅt, hfL‚ü©
  rcases hg x with ‚ü®Kg, t‚ÇÇ, h‚ÇÇt, hgL‚ü©
  use Kf + Kg, t‚ÇÅ ‚à© t‚ÇÇ
  have hf' : LipschitzOnWith Kf f (t‚ÇÅ ‚à© t‚ÇÇ) := hfL.mono (Set.inter_subset_left t‚ÇÅ t‚ÇÇ)
  have hg' : LipschitzOnWith Kg g (t‚ÇÅ ‚à© t‚ÇÇ) := hgL.mono (Set.inter_subset_right t‚ÇÅ t‚ÇÇ)
  exact ‚ü®Filter.inter_mem h‚ÇÅt h‚ÇÇt, hf'.sum hg'‚ü©

-- this one should definitely be in mathlib!
lemma helper (a b : ‚Ñù) : ENNReal.ofReal (a * b) = ENNReal.ofReal a * ENNReal.ofReal b := by sorry

-- FIXME: there should be a nicer solution than the ugly cast!
protected lemma LipschitzOnWith.smul {K : ‚Ñù‚â•0} {s : Set X} (hf : LipschitzOnWith K f s)
    (a : ‚Ñù) : LipschitzOnWith (ENNReal.toNNReal (ENNReal.ofReal ‚Äña‚Äñ) * K) (fun x ‚Ü¶ a ‚Ä¢ f x) s := by
  intro x hx y hy
  have : dist (a ‚Ä¢ f x) (a ‚Ä¢ f y) = ‚Äña‚Äñ * dist (f x) (f y) := by
      calc dist (a ‚Ä¢ f x) (a ‚Ä¢ f y)
        _ = ‚Äñ(a ‚Ä¢ (f x)) - (a ‚Ä¢ (f y))‚Äñ := by apply dist_eq_norm
        _ = ‚Äña ‚Ä¢ ((f x) - (f y))‚Äñ := by rw [smul_sub]
        _ = ‚Äña‚Äñ * ‚Äñ(f x) - (f y)‚Äñ := by rw [norm_smul]
        _ = ‚Äña‚Äñ * dist (f x) (f y) := by rw [‚Üê dist_eq_norm]
        _ = ‚Äña‚Äñ * dist (f x) (f y) := by rw [‚Üê dist_smul‚ÇÄ]
  calc edist (a ‚Ä¢ f x) (a ‚Ä¢ f y)
      _ = ENNReal.ofReal (dist (a ‚Ä¢ f x) (a ‚Ä¢ f y)) := by rw [edist_dist]
      _ = ENNReal.ofReal (‚Äña‚Äñ * dist (f x) (f y)) := by rw [this]
      _ = ENNReal.ofReal (‚Äña‚Äñ) * ENNReal.ofReal (dist (f x) (f y)) := by rw [‚Üê helper]
      _ = ENNReal.ofReal ‚Äña‚Äñ * edist (f x) (f y) := by rw [edist_dist]
      _ = ENNReal.toNNReal (ENNReal.ofReal ‚Äña‚Äñ) * edist (f x) (f y) := by
        sorry -- ‚Äña‚Äñ is non-negative, so these are the same
      _ ‚â§ ENNReal.toNNReal (ENNReal.ofReal ‚Äña‚Äñ) * (K * edist x y) := by exact mul_le_mul_left' (hf hx hy) _
      _ ‚â§ ‚Üë(ENNReal.toNNReal (ENNReal.ofReal ‚Äña‚Äñ) * K) * edist x y := by sorry --exact?-- sorry -- some conversion shenenigans

protected lemma LipschitzWith.smul {K : ‚Ñù‚â•0} (hf : LipschitzWith K f) {a : ‚Ñù} :
    LipschitzWith (ENNReal.toNNReal (ENNReal.ofReal ‚Äña‚Äñ)  * K) (fun x ‚Ü¶ a ‚Ä¢ f x) :=
  lipschitzOn_univ.mp ((lipschitzOn_univ.mpr hf).smul a)

/-- Multiplying a locally Lipschitz function by a constant remains locally Lipschitz. -/
protected lemma LocallyLipschitz.mysmul (hf : LocallyLipschitz f) {a : ‚Ñù} : LocallyLipschitz (fun x ‚Ü¶ a ‚Ä¢ f x) := by
  intro x
  rcases hf x with ‚ü®Kf, t, ht, hfL‚ü©
  exact ‚ü®ENNReal.toNNReal (ENNReal.ofReal ‚Äña‚Äñ) * Kf, t, ht, hfL.smul a‚ü©
end Normed
