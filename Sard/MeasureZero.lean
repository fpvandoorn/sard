/-
Copyright (c) 2023 Michael Rothgang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Author: Michael Rothgang.
-/
import Mathlib.Geometry.Manifold.SmoothManifoldWithCorners
import Mathlib.Topology.MetricSpace.Polish
import Mathlib.MeasureTheory.Measure.Haar.Basic
import Sard.Meagre

/-!
# Measure zero subsets of a manifold

Defines the notion of "measure zero" subsets on a finite-dimensional topological manifold.

Let $M$ be a finite-dimensional manifold. While $M$ is a measurable space (for instance, pull back the Lebesgue measure on each chart and use a partition of unity) , it has no canonical measure. However, there is a natural notion of *measure zero* subsets of $M$: a subset $A\subset M$ has measure zero iff for each chart $(\phi, U)$ of $M$ the set $\phi(U\cap A)\subset R^n$ has measure zero (w.r.t. to any additive Haar measure).

Measure zero sets are closed under subsets and countable unions, hence their complement defines the *almost everywhere filter* on a manifold. Measure zero subsets have empty interior, hence closed measure zero sets are nowhere dense.

**TODO**
- show that if $M$ is a normed space with Haar measure $\mu$, a set $A ‚äÜ M$ has measure zero iff $Œº A = 0$.
- show the same if $M$ is a manifold with a suitable measure $\mu$. If `MeasureZero` were defined using `IsOpenPosMeasure` on a chart, that would probably be a suitable class on `M`.
- include manifolds with boundary: in `open_implies_empty`, one needs to show that an open subset of `M` includes an interior point of `M`. (The current definition of manifolds with boundary seems to be too general for that.)


## Main results
- `MeasureZero`: Prop for measure zero subsets in `M`
- `MeasureZero.mono`: measure zero subsets are closed under subsets
- `MeasureZero.union` and `MeasureZero.iUnion`: measure zero subsets are closed under countable unions
- `MeasureZero.ae`: the complements of measure zero sets form a filter, the **almost everywhere filter**.
- `open_implies_empty`: an open measure zero subset is empty
- `MeasureZero_implies_empty_interior`: a measure zero subset has empty interior.

## References
See [Hirsch76], chapter 3.1 for the definition of measure zero subsets in a manifold.
-/

open MeasureTheory Measure Function TopologicalSpace Set
set_option autoImplicit false

variable
  -- Let `M` be a finite-dimensional (topological) manifold without boundary over the pair `(E, H)`.
  {E : Type*} {ùïÇ : Type*} [IsROrC ùïÇ] [NormedAddCommGroup E] [NormedSpace ùïÇ E] {H : Type*} [TopologicalSpace H]
  (I : ModelWithCorners ùïÇ E H) {M : Type*} [TopologicalSpace M] [ChartedSpace H M] [I.Boundaryless]
  [FiniteDimensional ùïÇ E] [MeasurableSpace E] [BorelSpace E]
variable {I}

variable (I) in
/-- A measure zero subset of an n-dimensional manifold $M$ is a subset $S ‚äÜ M$ such that
for all charts $(œÜ, U)$ of $M$, $œÜ(U ‚à© S) ‚äÜ ùïÇ^n$ has measure zero. -/
def MeasureZero (s : Set M) : Prop :=
  ‚àÄ (Œº : Measure E) [IsAddHaarMeasure Œº], ‚àÄ e ‚àà atlas H M, Œº (I ‚àò e '' (e.source ‚à© s)) = 0

namespace MeasureZero
/-- Having measure zero is monotone: a subset of a set with measure zero has measure zero. -/
protected lemma mono {s t : Set M} (hst : s ‚äÜ t) (ht : MeasureZero I t) :
    (MeasureZero I s) := by
  intro Œº hŒº e he
  have : I ‚àò e '' (e.source ‚à© s) ‚äÜ  I ‚àò e '' (e.source ‚à© t) := by
    apply image_subset
    exact inter_subset_inter_right e.source hst
  exact measure_mono_null this (ht Œº e he)

/-- The empty set has measure zero. -/
protected lemma empty : MeasureZero I (‚àÖ: Set M) := by
  intro Œº _ e _
  simp only [comp_apply, inter_empty, image_empty, OuterMeasure.empty']

/-- The countable index union of measure zero sets has measure zero. -/
protected lemma iUnion {Œπ : Type*} [Encodable Œπ] {s : Œπ ‚Üí Set M}
  (hs : ‚àÄ n : Œπ, MeasureZero I (s n)) : MeasureZero I (‚ãÉ (n : Œπ),  s n) := by
  intro Œº hŒº e he
  have : I ‚àò e '' (e.source ‚à© (‚ãÉ (n : Œπ),  s n)) = ‚ãÉ (n : Œπ), I ‚àò e '' (e.source ‚à© s n) := by
    rw [inter_iUnion]
    exact image_iUnion
  -- union of null sets is a null set
  simp_all only [comp_apply, OuterMeasure.iUnion_null_iff]
  intro i
  apply hs
  exact he

/-- The finite union of measure zero sets has measure zero. -/
protected lemma union {s t : Set M} (hs : MeasureZero I s) (ht : MeasureZero I t) : MeasureZero I (s ‚à™ t) := by
  let u : Bool ‚Üí Set M := fun b ‚Ü¶ cond b s t
  have : ‚àÄ i : Bool, MeasureZero I (u i) := by
    intro i
    cases i
    ¬∑ exact ht
    ¬∑ exact hs
  rw [union_eq_iUnion]
  exact MeasureZero.iUnion this

/-- The ‚Äúalmost everywhere‚Äù filter of co-measure zero sets in a manifold. -/
def ModelWithCorners.ae
    {E : Type*} [NormedAddCommGroup E] [NormedSpace ùïÇ E]
    {F : Type*} [TopologicalSpace F] (I : ModelWithCorners ùïÇ E F)
    {M : Type*} [TopologicalSpace M] [ChartedSpace F M] [MeasurableSpace E] : Filter M where
  sets := { s | MeasureZero I s·∂ú }
  univ_sets := by
    rw [mem_setOf, compl_univ]
    apply MeasureZero.empty
  inter_sets hx hy:= by
    simp only [mem_setOf_eq] at *
    rw [compl_inter]
    exact hx.union hy
  sets_of_superset hs hst := hs.mono (Iff.mpr compl_subset_compl hst)

/-- An open set of measure zero is empty. -/
protected lemma open_implies_empty {s : Set M} (h‚ÇÅs : IsOpen s) (h‚ÇÇs : MeasureZero I s): s = ‚àÖ := by
  suffices ‚àÄ e ‚àà atlas H M, (e.source ‚à© s) = ‚àÖ by
    by_contra h
    obtain ‚ü®x, hx‚ü© : Set.Nonempty s := Iff.mp nmem_singleton_empty h
    specialize this (chartAt H x) (chart_mem_atlas H x)
    have h‚ÇÇ: x ‚àà (chartAt H x).toLocalEquiv.source ‚à© s := by
      simp
      exact hx
    rw [this] at h‚ÇÇ
    contradiction

  intro e he
  -- choose any Haar measure Œº
  have : WeaklyLocallyCompactSpace E := sorry -- TODO: over ‚Ñù, this instance is found, but not over ùïÇ
  obtain ‚ü®K''‚ü© : Nonempty (PositiveCompacts E) := PositiveCompacts.nonempty'
  let Œº : Measure E := addHaarMeasure K''
  -- by h‚ÇÇs Œº e, we have Œº (I‚àòe '' s) = 0
  specialize h‚ÇÇs Œº e he
  by_contra h
  -- in particular, e.source ‚à© s is an open subset contained in that, hence has measure zero
  have h' : Set.Nonempty (interior (I ‚àò e '' (e.source ‚à© s))) := by
    have : Set.Nonempty (I ‚àò e '' (e.source ‚à© s)) := by
      exact (Iff.mp Set.nmem_singleton_empty h).image _
    have : IsOpen (e '' (e.source ‚à© s)) := by
        apply e.image_open_of_open'
        exact h‚ÇÅs
    have : IsOpen (I ‚àò e '' (e.source ‚à© s)) := by
      rw [Set.image_comp]
      apply I.toHomeomorph.isOpenMap _ this
    rwa [this.interior_eq]
  apply (measure_pos_of_nonempty_interior (Œº := Œº) h').ne' h‚ÇÇs

/-- A subset of a manifold `M` with measure zero has empty interior.

In particular, a *closed* measure zero subset of M is nowhere dense.
(Closedness is required: there are generalised Cantor sets of positive Lebesgue measure.) -/
protected lemma MeasureZero_implies_empty_interior {s : Set M}
    (h‚ÇÇs : MeasureZero I s) : interior s = ‚àÖ :=
  (h‚ÇÇs.mono interior_subset).open_implies_empty isOpen_interior
end MeasureZero

---------------------------------------------------------
-- everything above the fold was already PRed to mathlib
---------------------------------------------------------

namespace MeasureZero
variable
  -- declare a smooth manifold `M` over the pair `(E, H)`.
  {E : Type*} {ùïÇ : Type*} [IsROrC ùïÇ]
  [NormedAddCommGroup E] [NormedSpace ùïÇ E] {H : Type*} [TopologicalSpace H]
  (I : ModelWithCorners ùïÇ E H) {M : Type*} [TopologicalSpace M] [ChartedSpace H M]
  [SmoothManifoldWithCorners I M] [FiniteDimensional ùïÇ E] [SecondCountableTopology M]
  -- declare a smooth manifold `N` over the pair `(F, G)`.
  {F : Type*}
  [NormedAddCommGroup F] [NormedSpace ùïÇ F] {G : Type*} [TopologicalSpace G]
  (J : ModelWithCorners ùïÇ F G) {N : Type*} [TopologicalSpace N] [ChartedSpace G N] [J.Boundaryless]
  [SmoothManifoldWithCorners J N] [FiniteDimensional ùïÇ F]
  [MeasurableSpace F] [BorelSpace F]

/-- The image `f(s)` of a set `s ‚äÜ M` under a C¬π map `f : M ‚Üí N` has measure zero
iff for each chart $(œÜ, U)$ of $M$, the image $f(U ‚à© s)$ has measure zero. -/
-- is the converse useful or just busywork?
lemma measure_zero_image_iff_chart_domains {f : M ‚Üí N} {s : Set M}
    (hs : ‚àÄ e ‚àà atlas H M, MeasureZero J (f '' (e.source ‚à© s))) : MeasureZero J (f '' s) := by
  -- The charts of M form an open cover.
  let U : M ‚Üí Set M := fun x ‚Ü¶ (ChartedSpace.chartAt x : LocalHomeomorph M H).source
  have hcovering : univ ‚äÜ ‚ãÉ (x : M), U x := by
    intro x
    have : x ‚àà U x := mem_chart_source H x
    rw [mem_iUnion]
    intro _
    use x
  have hopen : ‚àÄ x : M, IsOpen (U x) := fun x => (ChartedSpace.chartAt x).open_source
  -- Since M is second countable, it is Lindel√∂f: there is a countable subcover U_n of M.
  let ‚ü®T, ‚ü®hTCountable, hTcover‚ü©‚ü© := TopologicalSpace.isOpen_iUnion_countable U hopen
  -- Each f(U_n ‚à© S) has measure zero.
  have : ‚àÄ i : T, MeasureZero J (f '' ((U i) ‚à© s)) := by
    intro i
    let e : LocalHomeomorph M H := ChartedSpace.chartAt i
    have h : MeasureZero J (f '' (e.source ‚à© s)) := hs e (chart_mem_atlas H _)
    have h‚ÇÉ : U i = e.source := by rw [‚Üê Filter.principal_eq_iff_eq]
    apply h.mono _
    apply image_subset
    rw [h‚ÇÉ]
  -- The countable union of measure zero sets has measure zero.
  have decomp : ‚ãÉ (i : T), f '' ((U i) ‚à© s) = f '' s :=
    calc ‚ãÉ (i : T), f '' ((U i) ‚à© s)
      _ = f '' (‚ãÉ (i : T), (U i) ‚à© s) := by rw [image_iUnion]
      _ = f '' ((‚ãÉ (i : T), (U i)) ‚à© s) := by rw [iUnion_inter]
      _ = f '' ((‚ãÉ (i : M) (_ : i ‚àà T), U i) ‚à© s) := by rw [iUnion_coe_set]
      _ = f '' ((‚ãÉ (i : M), U i) ‚à© s) := by rw [hTcover]
      _ = f '' (univ ‚à© s) := by rw [subset_antisymm (by simp) (hcovering)]
      _ = f '' s := by rw [univ_inter]
  rw [‚Üê decomp]
  have todo : Encodable T := by sorry --infer_instance
  apply MeasureZero.iUnion (Œπ := T) this

/-- A closed measure zero set is nowhere dense. -/
lemma MeasureZero.closed_implies_nowhere_dense {s : Set N} (h‚ÇÅ : MeasureZero J s)
    (h‚ÇÇ : IsClosed s) : IsNowhereDense s :=
  Iff.mpr (closed_nowhere_dense_iff h‚ÇÇ) (MeasureZero.MeasureZero_implies_empty_interior h‚ÇÅ)

/-- A œÉ-compact measure zero set is meagre (the countable union of nowhere dense sets). -/
lemma meagre_if_sigma_compact [T2Space N] {s : Set N} (h‚ÇÅs : MeasureZero J s) (h‚ÇÇs : IsSigmaCompact s) : IsMeagre s := by
  -- Decompose into countably many pieces, using h‚ÇÇs.
  obtain ‚ü®K, ‚ü®hcompact, hcover‚ü©‚ü© := h‚ÇÇs
  -- The countable union of nowhere dense sets is meagre.
  suffices ‚àÄ n : ‚Ñï, IsNowhereDense (K n) by
    rw [‚Üê hcover]
    use range K
    constructor
    ¬∑ rintro t ‚ü®n, hn‚ü©
      rw [‚Üê hn]
      exact this n
    ¬∑ simp [IsMeagre]
      exact ‚ü®countable_range K, fun i ‚Ü¶ subset_iUnion K i‚ü©
  intro n
  have : K n ‚äÜ s := by
    rw [‚Üê hcover]
    exact subset_iUnion K n
  have h : MeasureZero J (K n) := h‚ÇÅs.mono this
  exact MeasureZero.closed_implies_nowhere_dense J h (IsCompact.isClosed (hcompact n))
end MeasureZero
