import Sard.LocallyLipschitz
import Sard.ManifoldAux
import Sard.MeasureZero
import Mathlib.Topology.MetricSpace.HausdorffDimension
import Mathlib.Geometry.Manifold.Diffeomorph

open ENNReal NNReal FiniteDimensional Function Manifold MeasureTheory Measure Set
  SmoothManifoldWithCorners TopologicalSpace Topology LocallyLipschitz
set_option autoImplicit false

variable
  -- declare a smooth manifold `M` over the pair `(E, H)`.
  {E : Type*}
  [NormedAddCommGroup E] [NormedSpace â„ E] {H : Type*} [TopologicalSpace H]
  (I : ModelWithCorners â„ E H) {M : Type*} [TopologicalSpace M] [ChartedSpace H M] [I.Boundaryless]
  [SmoothManifoldWithCorners I M] [FiniteDimensional â„ E]
  [SecondCountableTopology M] [MeasurableSpace E] [BorelSpace E]
  -- declare a smooth manifold `N` over the pair `(F, G)`.
  {F : Type*}
  [NormedAddCommGroup F] [NormedSpace â„ F] {G : Type*} [TopologicalSpace G]
  {J : ModelWithCorners â„ F G} {N : Type*} [TopologicalSpace N] [ChartedSpace G N] [J.Boundaryless]
  [SmoothManifoldWithCorners J N] [FiniteDimensional â„ F]
  [MeasurableSpace F] [BorelSpace F]
variable {m n r : â„•} (hm : finrank â„ E = m) (hn : finrank â„ F = n) (hr : r > m-n)

/-- If $U âŠ† â„^m$ is open and $f : U â†’ â„^n$ is a $C^1$ map with `m < n`, $f(U)$ has measure zero. -/
lemma image_measure_zero_of_C1_dimension_increase' {g : E â†’ F} {U : Set E} (hU : IsOpen U)
    [MeasurableSpace F] [BorelSpace F] (Î½ : Measure F) [IsAddHaarMeasure Î½]
    (hg : ContDiffOn â„ 1 g U) (hmn : m < n) : Î½ (g '' U) = 0 := by
  -- FIXME: once MeasureZero is refactored, replace the Haar measure Î½
  -- by the Hausdorff (or Lebesgue) measure, and this step solves itself.
  have h : Î½ = Î¼H[n] := sorry
  rw [h]
  -- TODO: remove convexity hypothesis from `dimH_image_le`; split into many small pieces.)
  have : Convex â„ U := sorry
  -- This seems to be missing from mathlib.
  have h : dimH (univ : Set E) = m := sorry
  apply measure_zero_of_dimH_lt (d := n) rfl.absolutelyContinuous
  calc dimH (g '' U)
    _ â‰¤ dimH U := hg.dimH_image_le this rfl.subset
    _ â‰¤ m := h â–¸ dimH_mono (subset_univ U) -- should this be a separate lemma?
    _ < n := Nat.cast_lt.mpr hmn

/-- Local version of Sard's theorem. If $W âŠ† â„^m$ is open and $f: W â†’ â„^n$ is $C^r$,
the set of critical values has measure zero. -/
theorem sard_local {s w : Set E} {f : E â†’ F} (hw : IsOpen w) (hsw : s âŠ† w)
    (hf : ContDiffOn â„ r f w) {f' : E â†’ E â†’L[â„] F} (hf' : âˆ€ x âˆˆ s, HasFDerivWithinAt f (f' x) w x)
    (h'f' : âˆ€ x âˆˆ s, Â¬ Surjective (f' x)) (Î¼ : Measure F) [IsAddHaarMeasure Î¼] :
    Î¼ (f '' s) = 0 := by
  by_cases hyp: m < n
  Â· have hr : 1 â‰¤ (r : â„•âˆ) := Nat.one_le_cast.mpr (Nat.one_le_of_lt hr)
    have hless: Î¼ (f '' s) â‰¤ 0 := calc
      Î¼ (f '' s)
      _ â‰¤ Î¼ (f '' w) := measure_mono (image_subset f hsw)
      _ = 0 := image_measure_zero_of_C1_dimension_increase' hw Î¼ (hf.of_le hr) hyp
    simp only [nonpos_iff_eq_zero, zero_le] at hless âŠ¢
    exact hless
  Â· sorry

/-- Local version of Sard's theorem. If $W âŠ† â„^m$ is open and $f: W â†’ â„^n$ is $C^r$,
the set of critical values of `f` is a meagre set.
We phrase this for any closed set `s` of critical points of `f`; this is fine
as the critical set of `f` is closed. -/
theorem sard_local' {s w : Set E} {f : E â†’ F} (hw : IsOpen w) (hs : IsClosed s) (hsw : s âŠ† w)
    (hf : ContDiffOn â„ r f w) {f' : E â†’ E â†’L[â„] F} (hf' : âˆ€ x âˆˆ s, HasFDerivWithinAt f (f' x) w x)
    (h'f' : âˆ€ x âˆˆ s, Â¬ Surjective (f' x)) : IsMeagre (f '' s) := by
  obtain âŸ¨K''âŸ© : Nonempty (PositiveCompacts F) := PositiveCompacts.nonempty'
  let Î¼ : Measure F := addHaarMeasure K''
  have ass : Î¼ (f '' s) = 0 := sard_local hr hw hsw hf hf' h'f' Î¼

  -- `s` is closed, hence Ïƒ-compact --- thus so is f '' s.
  have : IsSigmaCompact s := isSigmaCompact_univ.of_isClosed_subset hs (subset_univ s)
  have : IsSigmaCompact (f '' s) := this.image_of_continuousOn (hf.continuousOn.mono hsw)
  exact meagre_of_sigma_compact_null this ass

/-- **Sard's theorem**. Let $M$ and $N$ be real $C^r$ manifolds of dimensions
$m$ and $n$, and $f : M â†’ N$ a $C^r$ map. If $r>\max{0, m-n}$,
the set of regular values of `f` has full measure.

Note that mathlib already contains a weaker version of Sard's theorem,
as `addHaar_image_eq_zero_of_det_fderivWithin_eq_zero` in the file `Mathlib.MeasureTheory.Function.Jacobian.Manifold`.
This local result implies the case $m=n$ for $r\geq 1$ (not hard to show).
However, note that the case $m > n$ requires a different proof: for $m>n$, the condition
$f\in C^1$ is not sufficient any more: Whitney (1957) constructed a CÂ¹ function
$$f : â„Â² â†’ â„$$ whose set of critical values contains an open set, thus has positive measure. -/
theorem sard {f : M â†’ N} (hf : ContMDiff I J r f)
    {f' : âˆ€x, TangentSpace I x â†’L[â„] TangentSpace J (f x)} {s : Set M}
    (hf' : âˆ€ x âˆˆ s, HasMFDerivAt I J f x (f' x))
    (h'f' : âˆ€ x âˆˆ s, Â¬ Surjective (f' x)) : MeasureZero J (f '' s) := by
  suffices hyp : âˆ€ x : M, MeasureZero J (f '' ((chartAt H x).source âˆ© s)) from
    MeasureZero.measure_zero_image_iff_chart_domains (J := J) hyp
  intro x
  let e := chartAt H x

  -- Reduce to images of chart domains, then apply `sard_local`.
  -- Tedious part: check that all hypotheses transfer to their local counterparts.
  intro Î¼ hÎ¼ y
  let e' := chartAt G y
  -- Data for the reduction to local coordinates.
  let w := I âˆ˜ e '' (e.source âˆ© f â»Â¹' e'.source)
  let s_better := I âˆ˜ e '' (s âˆ© e.source âˆ© f â»Â¹' e'.source)
  let f_local := (J âˆ˜ e') âˆ˜ f âˆ˜ (e.invFun âˆ˜ I.invFun)
  -- "Obvious" computations from my data.
  have hwopen : IsOpen w := by
    refine e.extend_isOpenMapOn_source I ?_ (inter_subset_left _ _)
    exact e.open_source.inter (e'.open_source.preimage hf.continuous)
  have hsw : s_better âŠ† w := by
    apply image_subset
    rw [inter_assoc]
    apply inter_subset_right s _
  have hsbetterâ‚€ : s_better âŠ† I âˆ˜ e '' e.source := by
    apply image_subset
    rw [inter_comm s, inter_assoc]
    exact inter_subset_left _ _
  have cor : (e.invFun âˆ˜ I.invFun âˆ˜ I âˆ˜ e) '' (s âˆ© e.source âˆ© f â»Â¹' e'.source) = s âˆ© e.source âˆ© f â»Â¹' e'.source := by
    have : (s âˆ© e.source âˆ© f â»Â¹' e'.source) âŠ† e.source := by
      rw [inter_comm s, inter_assoc]
      apply inter_subset_left
    apply e.extend_symm_leftInverse' _ this
  have hsbetter : e.invFun âˆ˜ I.invFun '' s_better = s âˆ© e.source âˆ© f â»Â¹' e'.source := by
    calc e.invFun âˆ˜ I.invFun '' s_better
      _ = (e.invFun âˆ˜ I.invFun) âˆ˜ I âˆ˜ e '' (s âˆ© e.source âˆ© f â»Â¹' e'.source) := by
        simp only [comp.assoc, image_comp]
      _ = (e.invFun âˆ˜ I.invFun âˆ˜ I âˆ˜ e) '' (s âˆ© e.source âˆ© f â»Â¹' e'.source) := by simp only [comp.assoc, image_comp]
      _ = s âˆ© e.source âˆ© f â»Â¹' e'.source := cor
  -- Inclusions about s_better, which are needed at some point in the proofs below.
  have hsbetterâ‚ : (e.invFun âˆ˜ I.invFun) '' s_better âŠ† s := by
    rw [hsbetter, inter_assoc]
    exact inter_subset_left s _
  have hsbetterâ‚‚ : (e.invFun âˆ˜ I.invFun) '' s_better âŠ† e.source := by
    rw [hsbetter]
    rw [inter_comm s, inter_assoc]
    exact inter_subset_left _ _

  have hw : (f âˆ˜ e.invFun âˆ˜ I.invFun) '' w âŠ† e'.source := calc
    (f âˆ˜ e.invFun âˆ˜ I.invFun) '' w
      = f '' (e.invFun âˆ˜ I.invFun '' w) := by rw [image_comp]
    _ = f '' (e.source âˆ© f â»Â¹' e'.source) := by sorry -- fully analogous to rw [hsbetter]
    _ âŠ† f '' (f â»Â¹' e'.source) := image_subset _ (inter_subset_right _ _)
    _ âŠ† e'.source := image_preimage_subset f e'.source
  have hsbetterâ‚ƒ : (f âˆ˜ e.invFun âˆ˜ I.invFun) '' s_better âŠ† e'.source := calc
    (f âˆ˜ e.invFun âˆ˜ I.invFun) '' s_better
    _ âŠ† (f âˆ˜ e.invFun âˆ˜ I.invFun) '' w := image_subset _ hsw
    _ âŠ† e'.source := hw
  have : J âˆ˜ e' '' (e'.source âˆ© f '' (e.source âˆ© s)) = f_local '' s_better := by
    symm
    calc f_local '' s_better
      _ = (J âˆ˜ e') âˆ˜ f '' ((e.invFun âˆ˜ I.invFun âˆ˜ I âˆ˜ e) '' (s âˆ© e.source âˆ© f â»Â¹' e'.source)) := by
        simp only [comp.assoc, image_comp]
      _ = J âˆ˜ e' '' (f '' (s âˆ© e.source âˆ© f â»Â¹' e'.source)) := by rw [cor, image_comp]
      _ = J âˆ˜ e' '' (f '' (e.source âˆ© s âˆ© f â»Â¹' e'.source)) := by rw [inter_comm s]
      _ = J âˆ˜ e' '' (f '' (e.source âˆ© s) âˆ© e'.source) := by rw [image_inter_preimage f _ _]
      _ = J âˆ˜ e' '' (e'.source âˆ© f '' (e.source âˆ© s)) := by rw [inter_comm]
  rw [this]
  apply sard_local hr (w := w) (s := s_better) (f := f_local) (f' := fderiv â„ f_local) (Î¼ := Î¼)
  Â· have : IsOpen (e.source âˆ© f â»Â¹' e'.source) :=
      IsOpen.inter e.open_source (e'.open_source.preimage hf.continuous)
    apply e.extend_isOpenMapOn_source _ this (inter_subset_left e.source _)
  Â· apply image_subset (â†‘I âˆ˜ â†‘e)
    rw [inter_assoc]
    exact inter_subset_right s (e.source âˆ© f â»Â¹' e'.source)
  Â· -- ContDiffOn â„ (â†‘r) f_local w follows by definition, of ContMDiff f in charts
    have he : e âˆˆ maximalAtlas I M := by apply subset_maximalAtlas; exact chart_mem_atlas H x
    have he' : e' âˆˆ maximalAtlas J N := by apply subset_maximalAtlas; exact chart_mem_atlas G y
    have hs : e.source âˆ© f â»Â¹' e'.source âŠ† e.source := inter_subset_left _ _
    have h2s : MapsTo f (e.source âˆ© f â»Â¹' e'.source) e'.source :=
      (mapsTo_preimage f e'.source).mono_left (inter_subset_right _ _)
    exact (contMDiffOn_iff_of_mem_maximalAtlas' (n := r) he he' hs h2s).mp hf.contMDiffOn
  Â· -- âˆ€ x âˆˆ s_better, HasFDerivWithinAt f_local (fderiv â„ f_local x) s_better x
    -- XXX: there's not much happening, surely this can be golfed!
    -- XXX: something like HasFDerivWithAt_iff_of_mem_maximalAtlas' would be super convenient
    intro xnew hx
    let x' := (e.invFun âˆ˜ I.invFun) xnew
    have hx'1 : x' âˆˆ s := hsbetterâ‚ (mem_image_of_mem _ hx)
    have hx'2 : x' âˆˆ e.source := hsbetterâ‚‚ (mem_image_of_mem _ hx)
    have hx'3 : f x' âˆˆ e'.source := hsbetterâ‚ƒ (mem_image_of_mem _ hx)
    specialize hf' x' hx'1
    -- (1) f_local is differentiable as f is: use charts
    obtain âŸ¨_, realâŸ© := (mdifferentiableAt_iff_of_mem_source hx'2 hx'3).mp hf'.mdifferentiableAt
    rw [I.range_eq_univ, differentiableWithinAt_univ] at real
    -- (2) recover the differential, using fderiv
    have : DifferentiableAt â„ f_local ((I âˆ˜ e) x') := real
    have h : (I âˆ˜ e) x' = xnew := e.extend_leftInverse _ (hsbetterâ‚€ hx)
    rw [h] at this
    exact (hasFDerivWithinAt_of_open hwopen (hsw hx)).mpr this.hasFDerivAt
  Â· -- âˆ€ x âˆˆ s_better, Â¬Surjective (fderiv â„ f_local x)
    intro x hx
    -- f_local is a map from E to F, hence its fderiv equals its mfderiv.
    rw [â† mfderiv_eq_fderiv]
    set D := mfderiv ğ“˜(â„, E) ğ“˜(â„, F) f_local x
    -- By definition, f_local is the composition (J âˆ˜ e') âˆ˜ f âˆ˜ e.invFun âˆ˜ I.invFun.
    -- Hence, by the chain rule, its mfderiv is the composition of those.
    let A := mfderiv ğ“˜(â„, E) I (e.invFun âˆ˜ I.invFun) x
    let B := mfderiv I J f ((e.invFun âˆ˜ I.invFun) x)
    let C := mfderiv J ğ“˜(â„, F) (J âˆ˜ e') ((f âˆ˜ e.invFun âˆ˜ I.invFun) x)
    -- Technical lemmas needed to apply the chain rule.
    -- **n**eighbourhood lemma for **A**
    have hnA : I âˆ˜ â†‘e '' e.source âˆˆ ğ“ x := by -- this is boring; consolidate these details!
      let x' := (e.invFun âˆ˜ I.invFun) x
      have : (I âˆ˜ e) x' = x := e.extend_leftInverse _ (hsbetterâ‚€ hx)
      rw [â† this]
      have : e.source âˆˆ ğ“ x' := by
        have : x' âˆˆ e.source := by
          sorry-- apply mem_of_mem_of_subset hx hsbetterâ‚€ --?h--sorry
          -- apply hsbetterâ‚€--apply inter_subset_left--refine MapsTo.image_subset ?h
        exact IsOpen.mem_nhds e.open_source this
      exact extendedChart_image_nhds_on I this (Eq.subset rfl)
    have hA : MDifferentiableAt ğ“˜(â„, E) I (e.invFun âˆ˜ I.invFun) x :=
      SmoothAt.mdifferentiableAt ((extendedChart_symm_smooth _ (chart_mem_atlas H _)).contMDiffAt hnA)
    -- General nonsense: f is ContMDiff, hence also MDifferentiable at each point.
    have hB : MDifferentiableAt I J f ((e.invFun âˆ˜ I.invFun) x) := sorry
    -- Should be similar.
    have hBA : MDifferentiableAt ğ“˜(â„, E) J (f âˆ˜ e.invFun âˆ˜ I.invFun) x := sorry
    -- should be obvious, skipping for now. (open set as w is open)
    have hnC : ((f âˆ˜ e.invFun âˆ˜ I.invFun) '' w) âˆˆ ğ“ ((f âˆ˜ e.invFun âˆ˜ I.invFun) x) := sorry
    have hC : MDifferentiableAt J ğ“˜(â„, F) (J âˆ˜ e') ((f âˆ˜ e.invFun âˆ˜ I.invFun) x) := by
      have : ContMDiffOn J ğ“˜(â„, F) âˆ (J âˆ˜ e') e'.source := extendedChart_smooth _ (chart_mem_atlas G _)
      have : ContMDiffOn J ğ“˜(â„, F) âˆ (J âˆ˜ e') ((f âˆ˜ e.invFun âˆ˜ I.invFun) '' w) := this.mono hw
      exact SmoothAt.mdifferentiableAt (this.contMDiffAt hnC)
    -- By the chain rule, D is the composition of A, B and C.
    let comp := C.comp (B.comp A)
    let r := calc comp
      _ = C.comp (B.comp A) := rfl
      _ = C.comp ((mfderiv I J f ((e.invFun âˆ˜ I.invFun) x)).comp (mfderiv ğ“˜(â„, E) I (e.invFun âˆ˜ I.invFun) x)) := rfl
      _ = C.comp (mfderiv ğ“˜(â„, E) J (f âˆ˜ (e.invFun âˆ˜ I.invFun)) x) := by rw [(mfderiv_comp x hB hA)]
      _ = (mfderiv J ğ“˜(â„, F) (J âˆ˜ e') ((f âˆ˜ e.invFun âˆ˜ I.invFun) x)).comp (mfderiv ğ“˜(â„, E) J (f âˆ˜ (e.invFun âˆ˜ I.invFun)) x) := rfl
      _ = mfderiv ğ“˜(â„, E) ğ“˜(â„, F) ((J âˆ˜ e') âˆ˜ ((f âˆ˜ e.invFun âˆ˜ I.invFun))) x := by rw [(mfderiv_comp x hC hBA)]
      _ = mfderiv ğ“˜(â„, E) ğ“˜(â„, F) f_local x := rfl
      _ = D := rfl

    -- By hypothesis, B is not surjective.
    let x' := (e.invFun âˆ˜ I.invFun) x
    have aux : x' âˆˆ s := hsbetterâ‚ (mem_image_of_mem _ hx)
    have : f' x' = B := by rw [â† (hf' x' aux).mfderiv]
    have hBsurj : Â¬ Surjective B := this â–¸ h'f' _ aux
    -- The charts I âˆ˜ e and J âˆ˜ e' are diffeos, hences their differentials are isomorphisms.
    -- Thus, A and C are bijective.
    have hA : Bijective A := extendedChart_symm_differential_bijective I (chart_mem_atlas H _) (hsbetterâ‚€ hx)
    have hC : Bijective C :=
      extendedChart_differential_bijective J (chart_mem_atlas G _) (hsbetterâ‚ƒ (mem_image_of_mem _ hx))
    -- Thus, B is surjective iff `comp` is.
    -- FUTURE: extract as lemma: df' is {injective,surjective} iff its composition in charts is.
    have : Surjective B â†” Surjective comp := by
      have : comp = (C âˆ˜ B) âˆ˜ A := rfl
      rw [this]
      rw [hA.surjective.of_comp_iff (C âˆ˜ B)]
      rw [Surjective.of_comp_iff' hC B]
    rw [â† r]
    have : Â¬Surjective comp := by rw [â† this]; exact hBsurj
    exact this

/-- **Sard's theorem**: let $M$ and $N$ be real $C^r$ manifolds of dimensions $m$ and $n$,
and $f:Mâ†’N$ a $C^r$ map. If $r>\max{0, m-n}$, the critical set is meagre. -/
-- FIXME: do I really need N to be Hausdorff? For the local result, I don't...
theorem sard' {f : M â†’ N} (hf : ContMDiff I J r f) [T2Space N]
    {f' : âˆ€x, TangentSpace I x â†’L[â„] TangentSpace J (f x)} {s : Set M} (hs : IsClosed s)
    (hf' : âˆ€ x âˆˆ s, HasMFDerivAt I J f x (f' x))
    (h'f' : âˆ€ x âˆˆ s, Â¬ Surjective (f' x)) : IsMeagre (f '' s) := by
  -- M is second countable and locally compact (as finite-dimensional), hence Ïƒ-compact.
  have : IsSigmaCompact s := isSigmaCompact_univ.of_isClosed_subset hs (subset_univ s)
  exact MeasureZero.meagre_if_sigma_compact J (sard _ hr hf hf' h'f') (this.image (hf.continuous))

-- Corollary. The set of regular values is residual and therefore dense.

-- most general version: phrased using the Hausdorff dimension
