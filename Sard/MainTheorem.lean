import Sard.ManifoldAux
import Sard.MeasureZero
import Mathlib.Topology.MetricSpace.HausdorffDimension
import Mathlib.Geometry.Manifold.Diffeomorph

open ENNReal NNReal FiniteDimensional Function Manifold MeasureTheory Measure Set
  SmoothManifoldWithCorners TopologicalSpace Topology LocallyLipschitz

variable
  -- declare a smooth manifold `M` over the pair `(E, H)`.
  {E : Type*}
  [NormedAddCommGroup E] [NormedSpace ‚Ñù E] {H : Type*} [TopologicalSpace H]
  (I : ModelWithCorners ‚Ñù E H) {M : Type*} [TopologicalSpace M] [ChartedSpace H M] --[I.Boundaryless]
  [SmoothManifoldWithCorners I M] [FiniteDimensional ‚Ñù E]
  [SecondCountableTopology M] [MeasurableSpace E] [BorelSpace E]
  -- declare a smooth manifold `N` over the pair `(F, G)`.
  {F : Type*}
  [NormedAddCommGroup F] [NormedSpace ‚Ñù F] {G : Type*} [TopologicalSpace G]
  {J : ModelWithCorners ‚Ñù F G} {N : Type*} [TopologicalSpace N] [ChartedSpace G N] --[J.Boundaryless]
  [SmoothManifoldWithCorners J N] [FiniteDimensional ‚Ñù F]
  [MeasurableSpace F] [BorelSpace F]
variable {m n r : ‚Ñï} (hm : finrank ‚Ñù E = m) (hn : finrank ‚Ñù F = n) (hr : r > m-n)

/-- If $U ‚äÜ ‚Ñù^m$ is open and $f : U ‚Üí ‚Ñù^n$ is a $C^1$ map with `m < n`, $f(U)$ has measure zero. -/
lemma image_measure_zero_of_C1_dimension_increase' {g : E ‚Üí F} {U : Set E} (hU : IsOpen U)
    [MeasurableSpace F] [BorelSpace F] (ŒΩ : Measure F) [IsAddHaarMeasure ŒΩ]
    (hg : ContDiffOn ‚Ñù 1 g U) (hmn : m < n) : ŒΩ (g '' U) = 0 := by
  -- FIXME: once MeasureZero is refactored, replace the Haar measure ŒΩ
  -- by the Hausdorff (or Lebesgue) measure, and this step solves itself.
  have h : ŒΩ = ŒºH[n] := sorry
  rw [h]
  -- TODO: remove convexity hypothesis from `dimH_image_le`; split into many small pieces.)
  have : Convex ‚Ñù U := sorry
  -- This seems to be missing from mathlib.
  have h : dimH (univ : Set E) = m := sorry
  apply measure_zero_of_dimH_lt (d := n) rfl.absolutelyContinuous
  calc dimH (g '' U)
    _ ‚â§ dimH U := hg.dimH_image_le this rfl.subset
    _ ‚â§ m := h ‚ñ∏ dimH_mono (subset_univ U) -- should this be a separate lemma?
    _ < n := Nat.cast_lt.mpr hmn

/-- Local version of Sard's theorem. If $W ‚äÜ ‚Ñù^m$ is open and $f: W ‚Üí ‚Ñù^n$ is $C^r$,
the set of critical values has measure zero. -/
theorem sard_local {s w : Set E} {f : E ‚Üí F} (hw : IsOpen w) (hsw : s ‚äÜ w)
    (hf : ContDiffOn ‚Ñù r f w) {f' : E ‚Üí E ‚ÜíL[‚Ñù] F} (hf' : ‚àÄ x ‚àà s, HasFDerivWithinAt f (f' x) w x)
    (h'f' : ‚àÄ x ‚àà s, ¬¨ Surjective (f' x)) (Œº : Measure F) [IsAddHaarMeasure Œº] :
    Œº (f '' s) = 0 := by
  by_cases hyp: m < n
  ¬∑ have hr : 1 ‚â§ (r : ‚Ñï‚àû) := Nat.one_le_cast.mpr (Nat.one_le_of_lt hr)
    have hless: Œº (f '' s) ‚â§ 0 := calc
      Œº (f '' s)
      _ ‚â§ Œº (f '' w) := measure_mono (image_subset f hsw)
      _ = 0 := image_measure_zero_of_C1_dimension_increase' hw Œº (hf.of_le hr) hyp
    simp only [nonpos_iff_eq_zero, zero_le] at hless ‚ä¢
    exact hless
  ¬∑ sorry

/-- Local version of Sard's theorem. If $W ‚äÜ ‚Ñù^m$ is open and $f: W ‚Üí ‚Ñù^n$ is $C^r$,
the set of critical values of `f` is a meagre set.
We phrase this for any closed set `s` of critical points of `f`; this is fine
as the critical set of `f` is closed. -/
theorem sard_local' {s w : Set E} {f : E ‚Üí F} (hw : IsOpen w) (hs : IsClosed s) (hsw : s ‚äÜ w)
    (hf : ContDiffOn ‚Ñù r f w) {f' : E ‚Üí E ‚ÜíL[‚Ñù] F} (hf' : ‚àÄ x ‚àà s, HasFDerivWithinAt f (f' x) w x)
    (h'f' : ‚àÄ x ‚àà s, ¬¨ Surjective (f' x)) : IsMeagre (f '' s) := by
  obtain ‚ü®K''‚ü© : Nonempty (PositiveCompacts F) := PositiveCompacts.nonempty'
  let Œº : Measure F := addHaarMeasure K''
  have ass : Œº (f '' s) = 0 := sard_local hr hw hsw hf hf' h'f' Œº

  -- `s` is closed, hence œÉ-compact --- thus so is f '' s.
  have : IsSigmaCompact s := isSigmaCompact_univ.of_isClosed_subset hs (subset_univ s)
  have : IsSigmaCompact (f '' s) := this.image_of_continuousOn (hf.continuousOn.mono hsw)
  exact IsMeagre.of_isSigmaCompact_null this ass

/-- **Sard's theorem**. Let $M$ and $N$ be real $C^r$ manifolds of dimensions
$m$ and $n$, and $f : M ‚Üí N$ a $C^r$ map. If $r>\max{0, m-n}$,
the set of regular values of `f` has full measure.

Note that mathlib already contains a weaker version of Sard's theorem,
as `addHaar_image_eq_zero_of_det_fderivWithin_eq_zero` in the file `Mathlib.MeasureTheory.Function.Jacobian.Manifold`.
This local result implies the case $m=n$ for $r\geq 1$ (not hard to show).
However, note that the case $m > n$ requires a different proof: for $m>n$, the condition
$f\in C^1$ is not sufficient any more: Whitney (1957) constructed a C¬π function
$$f : ‚Ñù¬≤ ‚Üí ‚Ñù$$ whose set of critical values contains an open set, thus has positive measure. -/
theorem sard_boundaryless {f : M ‚Üí N} (hf : ContMDiff I J r f) [I.Boundaryless] [J.Boundaryless]
    {f' : ‚àÄx, TangentSpace I x ‚ÜíL[‚Ñù] TangentSpace J (f x)} {s : Set M}
    (hf' : ‚àÄ x ‚àà s, HasMFDerivAt I J f x (f' x))
    (h'f' : ‚àÄ x ‚àà s, ¬¨ Surjective (f' x)) : MeasureZero J (f '' s) := by
  suffices hyp : ‚àÄ x : M, MeasureZero J (f '' ((chartAt H x).source ‚à© s)) from
    measure_zero_image_iff_chart_domains (J := J) hyp
  intro x
  let e := chartAt H x

  -- Reduce to images of chart domains, then apply `sard_local`.
  -- Tedious part: check that all hypotheses transfer to their local counterparts.
  intro Œº hŒº y
  let e' := chartAt G y
  -- Data for the reduction to local coordinates.
  let w := e.extend I '' (e.source ‚à© f ‚Åª¬π' e'.source)
  let s_better := (e.extend I) '' (s ‚à© e.source ‚à© f ‚Åª¬π' e'.source)
  let f_local := (J ‚àò e') ‚àò f ‚àò (e.extend I).symm
  -- "Obvious" computations from my data.
  have hwopen : IsOpen w := by
    refine e.extend_isOpenMapOn_source I ?_ inter_subset_left
    exact e.open_source.inter (e'.open_source.preimage hf.continuous)
  have hsw : s_better ‚äÜ w := by
    apply image_subset
    rw [inter_assoc]
    exact inter_subset_right
  have hsbetter‚ÇÄ : s_better ‚äÜ e.extend I '' e.source := by
    apply image_subset
    rw [inter_comm s, inter_assoc]
    exact inter_subset_left
  have cor : ((e.extend I).symm ‚àò e.extend I) '' (s ‚à© e.source ‚à© f ‚Åª¬π' e'.source) = s ‚à© e.source ‚à© f ‚Åª¬π' e'.source := by
    have : (s ‚à© e.source ‚à© f ‚Åª¬π' e'.source) ‚äÜ e.source := by
      rw [inter_comm s, inter_assoc]
      apply inter_subset_left
    apply e.extend_left_inv' _ this
  have hsbetter : (e.extend I).symm '' s_better = s ‚à© e.source ‚à© f ‚Åª¬π' e'.source := by
    calc (e.extend I).symm '' s_better
      _ = ((e.extend I).symm ‚àò (e.extend I)) '' (s ‚à© e.source ‚à© f ‚Åª¬π' e'.source) := by simp only [comp.assoc, image_comp]
      _ = s ‚à© e.source ‚à© f ‚Åª¬π' e'.source := cor
  -- Inclusions about s_better, which are needed at some point in the proofs below.
  have hsbetter‚ÇÅ : (e.extend I).symm '' s_better ‚äÜ s := by
    rw [hsbetter, inter_assoc]
    exact inter_subset_left
  have hsbetter‚ÇÇ : (e.extend I).symm '' s_better ‚äÜ e.source := by
    rw [hsbetter]
    rw [inter_comm s, inter_assoc]
    exact inter_subset_left

  have hw : (f ‚àò (e.extend I).symm) '' w ‚äÜ e'.source := calc
    (f ‚àò (e.extend I).symm) '' w
      = f '' ((e.extend I).symm '' w) := by rw [image_comp]
    _ = f '' (e.source ‚à© f ‚Åª¬π' e'.source) := by sorry -- fully analogous to rw [hsbetter]
    _ ‚äÜ f '' (f ‚Åª¬π' e'.source) := image_subset _ inter_subset_right
    _ ‚äÜ e'.source := image_preimage_subset f e'.source
  have hsbetter‚ÇÉ : (f ‚àò (e.extend I).symm) '' s_better ‚äÜ e'.source := calc
    (f ‚àò (e.extend I).symm) '' s_better
    _ ‚äÜ (f ‚àò (e.extend I).symm) '' w := image_subset _ hsw
    _ ‚äÜ e'.source := hw
  have : J ‚àò e' '' (e'.source ‚à© f '' (e.source ‚à© s)) = f_local '' s_better := by
    symm
    calc f_local '' s_better
      _ = ((J ‚àò e') ‚àò f) '' (((e.extend I).symm ‚àò (e.extend I)) '' (s ‚à© e.source ‚à© f ‚Åª¬π' e'.source)) := by
        simp only [e, e', f_local, s_better, comp.assoc, image_comp]
      _ = J ‚àò e' '' (f '' (s ‚à© e.source ‚à© f ‚Åª¬π' e'.source)) := by rw [cor, image_comp]
      _ = J ‚àò e' '' (f '' (e.source ‚à© s ‚à© f ‚Åª¬π' e'.source)) := by rw [inter_comm s]
      _ = J ‚àò e' '' (f '' (e.source ‚à© s) ‚à© e'.source) := by rw [image_inter_preimage f _ _]
      _ = J ‚àò e' '' (e'.source ‚à© f '' (e.source ‚à© s)) := by rw [inter_comm]
  rw [this]
  apply sard_local hr (w := w) (s := s_better) (f := f_local) (f' := fderiv ‚Ñù f_local) (Œº := Œº)
  ¬∑ exact e.extend_isOpenMapOn_source _
      (e.open_source.inter (e'.open_source.preimage hf.continuous)) inter_subset_left
  ¬∑ apply image_subset
    rw [inter_assoc]
    exact inter_subset_right
  ¬∑ -- ContDiffOn ‚Ñù (‚Üër) f_local w follows by definition, of ContMDiff f in charts
    have he : e ‚àà maximalAtlas I M := subset_maximalAtlas _ (chart_mem_atlas H x)
    have he' : e' ‚àà maximalAtlas J N := subset_maximalAtlas _ (chart_mem_atlas G y)
    have hs : e.source ‚à© f ‚Åª¬π' e'.source ‚äÜ e.source := inter_subset_left
    have h2s : MapsTo f (e.source ‚à© f ‚Åª¬π' e'.source) e'.source :=
      (mapsTo_preimage f e'.source).mono_left inter_subset_right
    exact (contMDiffOn_iff_of_mem_maximalAtlas' (n := r) he he' hs h2s).mp hf.contMDiffOn
  ¬∑ -- ‚àÄ x ‚àà s_better, HasFDerivWithinAt f_local (fderiv ‚Ñù f_local x) s_better x
    -- XXX: there's not much happening, surely this can be golfed!
    -- XXX: something like HasFDerivWithAt_iff_of_mem_maximalAtlas' would be super convenient
    intro xnew hx
    let x' := (e.extend I).symm xnew
    have hx'‚ÇÅ : x' ‚àà s := hsbetter‚ÇÅ (mem_image_of_mem _ hx)
    have hx'‚ÇÇ : x' ‚àà e.source := hsbetter‚ÇÇ (mem_image_of_mem _ hx)
    have hx'‚ÇÉ : f x' ‚àà e'.source := hsbetter‚ÇÉ (mem_image_of_mem _ hx)
    specialize hf' x' hx'‚ÇÅ
    -- (1) f_local is differentiable as f is: use charts
    obtain ‚ü®_, real‚ü© := (mdifferentiableAt_iff_of_mem_source hx'‚ÇÇ hx'‚ÇÉ).mp hf'.mdifferentiableAt
    rw [I.range_eq_univ, differentiableWithinAt_univ] at real
    -- (2) recover the differential, using fderiv
    have : DifferentiableAt ‚Ñù f_local (e.extend I x') := real
    have h : e.extend I x' = xnew := e.extend_right_inv _ (hsbetter‚ÇÄ hx)
    rw [h] at this
    exact (hasFDerivWithinAt_of_isOpen hwopen (hsw hx)).mpr this.hasFDerivAt
  ¬∑ -- ‚àÄ x ‚àà s_better, ¬¨Surjective (fderiv ‚Ñù f_local x)
    intro x hx
    -- f_local is a map from E to F, hence its fderiv equals its mfderiv.
    rw [‚Üê mfderiv_eq_fderiv]
    set D := mfderiv ùìò(‚Ñù, E) ùìò(‚Ñù, F) f_local x
    -- By definition, f_local is the composition (J ‚àò e') ‚àò f ‚àò e.invFun ‚àò I.invFun.
    -- Hence, by the chain rule, its mfderiv is the composition of those.
    let A := mfderiv ùìò(‚Ñù, E) I (e.extend I).symm x
    let B := mfderiv I J f ((e.extend I).symm x)
    let C := mfderiv J ùìò(‚Ñù, F) (e'.extend J) ((f ‚àò (e.extend I).symm) x)
    -- Technical lemmas needed to apply the chain rule.
    -- **n**eighbourhood lemma for **A**
    have hnA : e.extend I '' e.source ‚àà ùìù x := by -- this is boring; consolidate these details!
      let x' := (e.extend I).symm x
      have : e.extend I x' = x := e.extend_right_inv _ (hsbetter‚ÇÄ hx)
      rw [‚Üê this]
      have hesource : e.source ‚àà ùìù x' := by
        have : (e.extend I).symm x = x' := rfl
        have : x' ‚àà e.source := by sorry -- tedious busywork
          --rw [‚Üê this]
          --apply mem_of_mem_of_subset hx hsbetter‚ÇÄ --?h--sorry
          --apply inter_subset_left--refine MapsTo.image_subset ?h
        exact e.open_source.mem_nhds this
      exact extendedChart_image_nhds_on I hesource (Eq.subset rfl)
    have hr : 1 ‚â§ (r : ‚Ñï‚àû) := Nat.one_le_cast.mpr (Nat.one_le_of_lt hr)
    have he : e ‚àà maximalAtlas I M := by sorry -- why doesn't the same proof work?
    have : I '' e.target ‚àà ùìù x := sorry -- easy
    have hA : MDifferentiableAt ùìò(‚Ñù, E) I (e.invFun ‚àò I.invFun) x :=
      ((contMDiffOn_extend_symm (ùïú := ‚Ñù) he).contMDiffAt this).mdifferentiableAt hr
    -- General nonsense: f is ContMDiff, hence also MDifferentiable at each point.
    have hB : MDifferentiableAt I J f ((e.invFun ‚àò I.invFun) x) := hf.contMDiffAt.mdifferentiableAt hr
    -- Should be similar. TODO: fix this.
    have hBA : MDifferentiableAt ùìò(‚Ñù, E) J (f ‚àò (e.invFun ‚àò I.invFun)) x := by
      sorry -- exact hB.comp hA (M' := M) (M'' := N)
    -- should be obvious, skipping for now. (open set as w is open)
    have hnC : ((f ‚àò e.invFun ‚àò I.invFun) '' w) ‚àà ùìù ((f ‚àò e.invFun ‚àò I.invFun) x) := sorry
    have hC : MDifferentiableAt J ùìò(‚Ñù, F) (J ‚àò e') ((f ‚àò e.invFun ‚àò I.invFun) x) := by
      have : ContMDiffOn J ùìò(‚Ñù, F) ‚àû (J ‚àò e') e'.source := extendedChart_smooth _ (chart_mem_atlas G _)
      exact SmoothAt.mdifferentiableAt ((this.mono hw).contMDiffAt hnC)
    -- By the chain rule, D is the composition of A, B and C.
    let comp := C.comp (B.comp A)
    let r := calc comp
      _ = C.comp (B.comp A) := rfl
      _ = C.comp ((mfderiv I J f ((e.invFun ‚àò I.invFun) x)).comp (mfderiv ùìò(‚Ñù, E) I (e.invFun ‚àò I.invFun) x)) := rfl
      _ = C.comp (mfderiv ùìò(‚Ñù, E) J (f ‚àò (e.invFun ‚àò I.invFun)) x) := by rw [(mfderiv_comp x hB hA)]
      _ = (mfderiv J ùìò(‚Ñù, F) (J ‚àò e') ((f ‚àò e.invFun ‚àò I.invFun) x)).comp (mfderiv ùìò(‚Ñù, E) J (f ‚àò (e.invFun ‚àò I.invFun)) x) := rfl
      _ = mfderiv ùìò(‚Ñù, E) ùìò(‚Ñù, F) ((J ‚àò e') ‚àò ((f ‚àò e.invFun ‚àò I.invFun))) x := by rw [(mfderiv_comp x hC hBA)]
      _ = mfderiv ùìò(‚Ñù, E) ùìò(‚Ñù, F) f_local x := rfl
      _ = D := rfl

    -- By hypothesis, B is not surjective.
    let x' := (e.extend I).symm x
    have aux : x' ‚àà s := hsbetter‚ÇÅ (mem_image_of_mem _ hx)
    have : f' x' = B := by rw [‚Üê (hf' x' aux).mfderiv]
    have hBsurj : ¬¨ Surjective B := this ‚ñ∏ h'f' _ aux
    -- The charts e.extend I and e'.extend J are diffeos, hences their differentials are isomorphisms.
    -- Thus, A and C are bijective.
    have hA : Bijective A := extendedChart_symm_differential_bijective I (chart_mem_atlas H _) (hsbetter‚ÇÄ hx)
    have hC : Bijective C :=
      extendedChart_differential_bijective J (chart_mem_atlas G _) (hsbetter‚ÇÉ (mem_image_of_mem _ hx))
    -- Thus, B is surjective iff `comp` is.
    -- FUTURE: extract as lemma: df' is {injective,surjective} iff its composition in charts is.
    have : Surjective B ‚Üî Surjective comp := by
      have : comp = (C ‚àò B) ‚àò A := rfl
      rw [this]
      rw [hA.surjective.of_comp_iff (C ‚àò B)]
      rw [Surjective.of_comp_iff' hC B]
    rw [‚Üê r]
    have : ¬¨Surjective comp := by rw [‚Üê this]; exact hBsurj
    exact this

theorem sard {f : M ‚Üí N} (hf : ContMDiff I J r f)
    {f' : ‚àÄx, TangentSpace I x ‚ÜíL[‚Ñù] TangentSpace J (f x)} {s : Set M}
    (hf' : ‚àÄ x ‚àà s, HasMFDerivAt I J f x (f' x))
    (h'f' : ‚àÄ x ‚àà s, ¬¨ Surjective (f' x)) : MeasureZero J (f '' s) := by
  -- define interior and boundary of a manifold; show they decompose M
  -- show: interior is open, hence is a manifold without boundary
  -- (show: boundary is a one-dimensional submanifold --- not important for this theorem)
  -- show: boundary has measure zero
  -- deduce version this version from `sard_boundaryless`
  sorry

/-- **Sard's theorem**: let $M$ and $N$ be real $C^r$ manifolds of dimensions $m$ and $n$,
and $f:M‚ÜíN$ a $C^r$ map. If $r>\max{0, m-n}$, the critical set is meagre. -/
-- FIXME: do I really need N to be Hausdorff? For the local result, I don't...
-- FIXME: allow `N` with boundary - will follow once `meagre_if_sigma_compact
theorem sard' {f : M ‚Üí N} (hf : ContMDiff I J r f) [T2Space N] [J.Boundaryless]
    {f' : ‚àÄx, TangentSpace I x ‚ÜíL[‚Ñù] TangentSpace J (f x)} {s : Set M} (hs : IsClosed s)
    (hf' : ‚àÄ x ‚àà s, HasMFDerivAt I J f x (f' x))
    (h'f' : ‚àÄ x ‚àà s, ¬¨ Surjective (f' x)) : IsMeagre (f '' s) := by
  -- M is second countable and locally compact (as finite-dimensional), hence œÉ-compact.
  have : LocallyCompactSpace M := Manifold.locallyCompact_of_finiteDimensional I
  have : IsSigmaCompact s := isSigmaCompact_univ.of_isClosed_subset hs (subset_univ s)
  exact MeasureZero.isMeagre_of_isSigmaCompact J (sard I hf hf' h'f') (this.image (hf.continuous))

-- Corollary. The set of regular values is residual and therefore dense.

-- most general version: phrased using the Hausdorff dimension
